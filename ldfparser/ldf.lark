//
// MIT License
//
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions:
//
// The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software.
//
// THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.
//
// Source: https://github.com/uCAN-LIN/LinUSBConverter/blob/29289c854a6cb63ea7191d9cf020526d4e4e48fc/python_lib/ucanlintools/ldf.lark
//
start: ldf_container

?ldf_container: ldf_header [(ldf_nodes | ldf_signals | ldf_frames | ldf_diagnostic | ldf_diagnostic_frames | ldf_node_atributes | ldf_schedule_table | ldf_signal_encoding_types | ldf_signal_representation)*]

?ldf_nodes : "Nodes" "{" [ldf_node_master] [ldf_node_slaves] "}" 
?ldf_signals : "Signals" "{" [ldf_signal (ldf_signal)*] "}"  
?ldf_frames : "Frames" "{" [ldf_frame (ldf_frame)*] "}" 

?ldf_diagnostic : "Diagnostic_signals" "{" [(ANY_SEMICOLON_TERMINATED_LINE)*] "}"
?ldf_diagnostic_frames : "Diagnostic_frames" "{" [(ldf_diagnostic_master_req | ldf_diagnostic_slave_resp)*] "}"

?ldf_diagnostic_master_req: "MasterReq:" [C_INT] "{" [(ANY_SEMICOLON_TERMINATED_LINE)*] "}"
?ldf_diagnostic_slave_resp: "SlaveResp:" [C_INT] "{" [(ANY_SEMICOLON_TERMINATED_LINE)*] "}"

?ldf_node_atributes: "Node_attributes" "{" ldf_node_atributes_node? "}"
?ldf_node_atributes_node: ANY_OPENED_BLOCK [ANY_SEMICOLON_TERMINATED_LINE*] ANY_OPENED_BLOCK [ANY_SEMICOLON_TERMINATED_LINE*] "}" "}"

?ldf_schedule_table: "Schedule_tables" "{" [ldf_schedule_table_node*] "}" 
?ldf_schedule_table_node: ANY_OPENED_BLOCK [ANY_SEMICOLON_TERMINATED_LINE*] "}"

?ldf_signal_encoding_types: "Signal_encoding_types" "{" [ldf_signal_encoding_type*] "}" 
?ldf_signal_encoding_type: [ldf_signal_encoding_type_name] "{" [(ldf_signal_encoding_logical_value | ldf_signal_encoding_physical_value)*] ["}"] [ANY_CLOSED_BLOCK]
?ldf_signal_encoding_logical_value: "logical_value" "," [ldf_encoding_logical_signal_value] ("," [ldf_encoding_logical_signal_info])? ";"
?ldf_signal_encoding_physical_value: "physical_value" "," [ldf_encoding_phy_min] "," [ldf_encoding_phy_max] "," [ldf_encoding_phy_scale] "," [ldf_encoding_phy_offset] ("," ldf_encoding_phy_unit)? ";"

ldf_signal_encoding_type_name: CNAME
ldf_encoding_logical_signal_value: C_INT
ldf_encoding_logical_signal_info: ESCAPED_STRING

ldf_encoding_phy_min: C_INT
ldf_encoding_phy_max: C_INT
ldf_encoding_phy_scale: C_FLOAT
ldf_encoding_phy_offset: C_FLOAT
ldf_encoding_phy_unit: ESCAPED_STRING

?ldf_signal_representation: "Signal_representation" "{" [ldf_signal_representation_node*] "}" 
?ldf_signal_representation_node: ldf_signal_encoding_type_name ":" ldf_signal_name ("," ldf_signal_name)* ";"

?ldf_node_master: "Master:" ldf_node_name "," /.*;/  
?ldf_node_slaves: "Slaves:" ldf_node_name? ";" 

ldf_node : ldf_signal_name  ":" ldf_node_name ";" 
ldf_signal :  ldf_signal_name ":" ldf_signal_size "," ldf_signal_default_value "," ldf_node_name "," ldf_node_name ";" 
ldf_frame : ldf_frame_name  ":" ldf_frame_id "," ldf_node_name "," ldf_frame_len ["{" (ldf_frame_signal)* "}"] 


ldf_frame_signal: ldf_signal_name "," ldf_signal_bit_offset ";" 
ldf_frame_name: CNAME 
ldf_frame_id : C_INT 
ldf_frame_len : C_INT 

ldf_signal_default_value: C_INITIALIZER_LIST|C_INT 
ldf_signal_size: C_INT 
ldf_signal_bit_offset: C_INT 
ldf_signal_name: CNAME 

ldf_node_name: CNAME 

ANY_OPENED_BLOCK: /.*{/
ANY_CLOSED_BLOCK: /.*}/

ANY_SEMICOLON_TERMINATED_LINE: /.*;/
ANY_COLON_TERMINATED_LINE: /.*,/

LINE_COMMENT: /\/\/.*/
%ignore LINE_COMMENT

BLOCK_COMMENT_BEGIN: "/*" 
BLOCK_COMMENT_END: "*/"
BLOCK_COMMENT: BLOCK_COMMENT_BEGIN /(.*)/ BLOCK_COMMENT_END
%ignore BLOCK_COMMENT

C_INITIALIZER_LIST: ("{"|"{ ") C_INT ([","|", "]C_INT)* ("}"|" }")
C_INT: ("0x"HEXDIGIT+) | ("-"? HEXDIGIT+) 
C_FLOAT: "-"? INT ("." INT)?

ldf_header: [ldf_description_file ldf_header_protocol_version_def ldf_header_language_version_def ldf_header_speed_def (ldf_header_channel)?]
ldf_description_file: "LIN_description_file" ";"
ldf_header_protocol_version_def: "LIN_protocol_version" "=" [lin_protocol_version] ";"
ldf_header_language_version_def: "LIN_language_version" "=" [lin_language_version] ";"
ldf_header_speed_def: "LIN_speed" "=" [lin_speed] ";"
ldf_header_channel : "Channel_name"/.*;/

lin_protocol_version: ESCAPED_STRING
lin_language_version: ESCAPED_STRING
lin_speed: C_FLOAT "kbps"

%import common._STRING_INNER

%import common.HEXDIGIT 
%import common.INT
%import common.WORD
%import common.CNAME
%import common.ESCAPED_STRING
%import common.SIGNED_NUMBER
%import common.WS
%import common.WS_INLINE
%ignore WS
%ignore WS_INLINE